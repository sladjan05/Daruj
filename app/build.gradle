plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
    id 'com.google.gms.google-services'
    id "com.google.protobuf" version "0.8.17"
    id 'kotlin-parcelize'
}

android {
    signingConfigs {
        release {
            def propertiesFile = rootProject.file("keystore.properties")
            def properties = new Properties()
            properties.load(new FileInputStream(propertiesFile))

            storeFile file(properties['storeFile'])
            keyAlias properties['keyAlias']
            keyPassword properties['keyPassword']
            storePassword properties['storePassword']
        }
    }

    compileSdk 33

    defaultConfig {
        applicationId "net.jsoft.daruj"
        minSdk 26
        targetSdk 32
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "net.jsoft.daruj.HiltTestRunner"
        vectorDrawables {
            useSupportLibrary true
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_11.toString()
    }

    buildFeatures {
        compose true
    }

    composeOptions {
        kotlinCompilerExtensionVersion "1.2.0"
    }

    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
}

dependencies {
    // Android
    implementation 'com.google.android.material:material:1.6.1'

    // AndroidX
    implementation 'androidx.core:core-ktx:1.8.0'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.5.1'

    // Android Appcompat
    implementation 'androidx.appcompat:appcompat:1.5.0'

    // Compose
    implementation 'androidx.compose.ui:ui:1.3.0-beta01'
    implementation 'androidx.compose.material3:material3:1.0.0-beta01'
    implementation 'androidx.compose.ui:ui-tooling-preview:1.2.1'
    implementation 'androidx.activity:activity-compose:1.5.1'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-compose:2.5.1'
    implementation 'androidx.navigation:navigation-compose:2.5.1'
    implementation 'androidx.compose.material:material-icons-extended:1.2.1'
    implementation 'androidx.hilt:hilt-navigation-compose:1.0.0'
    implementation 'androidx.navigation:navigation-compose:2.5.1'

    // Testing
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    androidTestImplementation 'androidx.compose.ui:ui-test-junit4:1.3.0-beta01'
    debugImplementation 'androidx.compose.ui:ui-tooling:1.3.0-beta01'
    debugImplementation 'androidx.compose.ui:ui-test-manifest:1.3.0-beta01'
    androidTestImplementation 'com.google.truth:truth:1.1.3'

    // Dagger - Hilt
    implementation 'com.google.dagger:hilt-android:2.42'
    kapt 'com.google.dagger:hilt-compiler:2.42'

    androidTestImplementation 'com.google.dagger:hilt-android-testing:2.42'
    kaptAndroidTest 'com.google.dagger:hilt-compiler:2.42'

    testImplementation 'com.google.dagger:hilt-android-testing:2.42'
    kaptTest 'com.google.dagger:hilt-compiler:2.42'

    // DataStore
    implementation 'androidx.datastore:datastore-core:1.0.0'
    implementation 'androidx.datastore:datastore-preferences:1.0.0'
    implementation 'com.google.protobuf:protobuf-javalite:3.19.2'

    // Kotlin
    implementation 'org.jetbrains.kotlin:kotlin-reflect:1.7.10'

    // Coroutines
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.6.4'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.4'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-play-services:1.6.4'

    // Coroutine Lifecycle Scopes
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.5.1'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.5.1'

    // Firebase
    implementation platform('com.google.firebase:firebase-bom:30.2.0')
    implementation 'com.google.firebase:firebase-analytics-ktx'
    implementation 'com.google.firebase:firebase-auth-ktx'
    implementation 'com.google.firebase:firebase-firestore-ktx'
    implementation 'com.google.firebase:firebase-storage-ktx'
    implementation 'com.google.firebase:firebase-functions-ktx'

    // Retrofit
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'

    // Splash Screen
    implementation 'androidx.core:core-splashscreen:1.0.0'

    // Coil
    implementation 'io.coil-kt:coil-compose:2.1.0'

    // Gson
    implementation 'com.google.code.gson:gson:2.8.9'

    // Cropping Library
    implementation 'com.github.krokyze:ucropnedit:2.2.8'

    // Accompanist
    implementation 'com.google.accompanist:accompanist-swiperefresh:0.26.1-alpha'
    implementation 'com.google.accompanist:accompanist-systemuicontroller:0.17.0'
    implementation 'com.google.accompanist:accompanist-navigation-animation:0.24.13-rc'
    implementation 'com.google.accompanist:accompanist-pager:0.22.0-rc'
    implementation 'com.google.accompanist:accompanist-permissions:0.26.2-beta'

    // WorkManager
    implementation 'androidx.work:work-runtime-ktx:2.7.1'
    implementation 'androidx.hilt:hilt-work:1.0.0'
    kapt 'androidx.hilt:hilt-compiler:1.0.0'

}

kapt {
    correctErrorTypes true
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.14.0'
    }

    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java {
                    option 'lite'
                }
            }
        }
    }
}